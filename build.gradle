/****************************************************************************
 Copyright 2016 github.com/straightway

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

 http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
 ****************************************************************************/

buildscript {
    ext.kotlin_version = '1.2.20'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'straightway'
version '0.1-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'idea'

tasks.idea.dependsOn(cleanIdea)

idea.project.ipr {
    withXml { provider ->
        def inspectionManager = provider.asNode().appendNode('component', [name: 'InspectionProjectProfileManager'])
        def profile = inspectionManager.appendNode('profile', [version: '1.0'])
        profile.appendNode('option', [name: 'myName', value: 'Project Default'])
        def classInspection = profile.appendNode('inspection_tool', [class: 'ClassName',
                                                                     enabled: 'true',
                                                                     level: 'WEAK WARNING',
                                                                     enabled_by_default: 'true'])
        classInspection.appendNode('option', [name: 'namePattern', value: '[A-Z][_A-Za-z\\d]*'])
        def funInspection = profile.appendNode('inspection_tool', [class: 'FunctionName',
                                                                   enabled: 'true',
                                                                   level: 'WEAK WARNING',
                                                                   enabled_by_default: 'true'])
        funInspection.appendNode('option', [name: 'namePattern', value: '[a-z][_A-Za-z\\d]*'])

        def copyrightManager = provider.asNode().appendNode('component', [name: 'CopyrightManager', default: ''])
        def copyright = copyrightManager.appendNode('copyright')
        copyright.appendNode('option', [name: 'myName', value: 'Apache License'])
        copyright.appendNode('option', [name: 'notice', value: 'Copyright 2016 github.com/straightway\n\n Licensed under the Apache License, Version 2.0 (the &quot;License&quot;);\n you may not use this file except in compliance with the License.\n You may obtain a copy of the License at\n\n http://www.apache.org/licenses/LICENSE-2.0\n\n Unless required by applicable law or agreed to in writing, software\n distributed under the License is distributed on an &quot;AS IS&quot; BASIS,\n WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n See the License for the specific language governing permissions and\n limitations under the License.'])
    }
}

repositories {
    mavenCentral()
}
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
